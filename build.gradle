buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "gradle.plugin.com.github.honourednihilist:gradle-postgresql-embedded:0.2.0"
    classpath "gradle.plugin.com.github.jlouns:gradle-cross-platform-exec-plugin:0.5.0"
  }
}

apply plugin: "com.github.honourednihilist.gradle-postgresql-embedded"
apply plugin: "com.github.jlouns.cpe"

import static groovy.io.FileType.DIRECTORIES
import static org.apache.tools.ant.taskdefs.condition.Os.*

def isWindows = isFamily(FAMILY_WINDOWS)
def hasPostgres = (System.env.PG_CONNECTION_STRING != null)

String pg_username = "postgres"
String pg_password = "postgres"
String pg_db_name = "test_db"
int pg_port = 54321

postgresEmbedded {
	version = "V9_6_3"
	host = "localhost"
	port = pg_port
	dbName = pg_db_name
	username = pg_username
	password = pg_password
	artifactStorePath = "./.embedpostgresql" // where PostgreSQL distributions are stored after downloading, inside home directory by default
	stopWhenBuildFinished = true
	timeoutMillisBeforeStop = isWindows ? 15000 : 0
}

String pg_connection= hasPostgres ? System.env.PG_CONNECTION_STRING :
    "postgres://${pg_username}:${pg_password}@localhost:${pg_port}/${pg_db_name}"
String projectDir=project.projectDir.toString()
String buildDir="${projectDir}/build"
String testOutputDir="${buildDir}/test"

int test_timeout = 15000

////////////////////////////////////////////
task npmBootstrap (type: CrossPlatformExec) {
  commandLine 'npm', 'install'
}

task toolsBootstrap (type: CrossPlatformExec) {
  commandLine 'npm', 'install'
  workingDir './tools/tymly-packager'
}

task makeTestOutputDir {
  mkdir testOutputDir
}

String tarballDir = "${buildDir}/tarball"
String bundleWorkDir = "${buildDir}/bundle"
String tarfileName = "${tarballDir}/tymly-${getDate()}.tgz"
def getDate() { new Date().format('yyyyMMdd-HHmmss') }

task createTarball (type: CrossPlatformExec,
                    dependsOn: [npmBootstrap, toolsBootstrap]) {
  doFirst {
    mkdir tarballDir
    mkdir bundleWorkDir
  }

  def prefix = isWindows ? '' : './'
  commandLine "${prefix}bundle", "${projectDir}", '--tar', tarfileName, '--work', bundleWorkDir
  workingDir './tools/tymly-packager/bin'
}

String coreDockerDir = './tools/tymly-dockerize-core'
String coreDockerTag = 'tymly-core'

task dropTarfileToDocker (type: Copy,
                          dependsOn: [createTarball]) {
  from tarfileName
  into coreDockerDir
  rename { fileName -> "bundle.tgz" }
} // dropTarfileToDocker

task dockerTagCore (type: CrossPlatformExec) {
  commandLine 'docker', 'tag', coreDockerTag, "wmfs/${coreDockerTag}"
} // dockerTagCore

////////////////////////////////////////////
def packages = []
['packages', 'plugins', 'blueprints'].each {
  name ->
  def dir = new File("./${name}")
  if (!dir.exists())
    return
  dir.traverse(type: DIRECTORIES, maxDepth: 0) {
    def pkg = new File(it, 'package.json')
    if (pkg.exists())
      packages << [it.name, it.toString()]
  }
}
def testTasks = []
def ci_testTasks = []
def cleanTasks = []
packages.each { p ->
  def testDependencies = ['bootstrap']
  if (!hasPostgres)
    testDependencies << 'startPostgres'

  def name = p[0]
  def dir = p[1]
  def testTaskName = "test${name.capitalize()}"
  def cleanTaskName = "clean${name.capitalize()}"

  testTasks << testTaskName
  ci_testTasks << "ci_${testTaskName}"
  cleanTasks << cleanTaskName

  task "${testTaskName}"(type: CrossPlatformExec,
           dependsOn: testDependencies) {
      group 'Test'
      description "Runs ${name} test suite"

      commandLine 'npm', 'run', 'test'
      workingDir "${dir}"

      environment PG_CONNECTION_STRING: pg_connection
      environment TIMEOUT: test_timeout
  }

  testDependencies << 'cleanTestFiles'
  testDependencies << 'makeTestOutputDir'

  task "ci_${testTaskName}"(type: CrossPlatformExec,
                        dependsOn: testDependencies) {
    commandLine 'npm', 'run', 'test', '--', '--reporter', 'xunit', '--reporter-options', "output=${testOutputDir}/TEST-${name}-result.xml"
    workingDir "${dir}"

    environment PG_CONNECTION_STRING: pg_connection
    environment TIMEOUT: test_timeout
  }

  task "${cleanTaskName}"(type: Delete) {
    group 'Clean'
    description 'Cleans ${name} node_modules'

    delete "${dir}/node_modules"
  }
}

/////////////////////////////////////
task cleanNodeModules (dependsOn: cleanTasks) {
  group 'Clean'
  description 'Removes all packages, plugin, and blueprint node_modules'
}

task cleanBuild (type: Delete) {
  group 'Clean'
  description 'Removes build directory'

  delete buildDir
}

task cleanTestFiles (type: Delete) {
  group 'Clean'
  description 'Cleans up TEST-*-results.xml files'

  delete testOutputDir
}
task clean (dependsOn: [cleanNodeModules, cleanTestFiles, cleanBuild]) {
  group 'Clean'
  description 'Discards all node_modules directories, cleans up TEST-*.xml files, removes build directory'
}

task bootstrap (type: CrossPlatformExec,
                dependsOn: [npmBootstrap]) {
  group 'Build'
  description 'Installs the npm dependencies'

  commandLine 'npm', 'run', 'bootstrap'
}

task test (dependsOn: testTasks) {
  group 'Build'
  description 'Runs whole test suite'
}

task ci_test (dependsOn: ci_testTasks) {
  group 'Build'
  description 'Runs whole test suite, generating JUnit style XML output'
}

task bundle (dependsOn: [createTarball, dropTarfileToDocker]) {
  group 'Build'
  description 'Creates an installable Tymly package'
} // bundle

task dockerBuild (type: CrossPlatformExec,
                  dependsOn: [dropTarfileToDocker]) {
  group 'Build'
  description 'Build a runnable Tymly Docker image'

  workingDir coreDockerDir
  commandLine 'docker', 'build', '--tag', coreDockerTag, '.'
} // dockerBuild

task dockerPush (type: CrossPlatformExec,
                 dependsOn: [dockerTagCore]) {
  group 'Build'
  description 'Push Tymly Docker images to Hub'

  commandLine 'docker', 'push', "wmfs/${coreDockerTag}"
} // dockerPush
