buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "gradle.plugin.com.github.honourednihilist:gradle-postgresql-embedded:0.2.0"
    classpath "gradle.plugin.com.github.jlouns:gradle-cross-platform-exec-plugin:0.5.0"
  }
}

apply plugin: "com.github.honourednihilist.gradle-postgresql-embedded"
apply plugin: "com.github.jlouns.cpe"

import static groovy.io.FileType.DIRECTORIES
import static org.apache.tools.ant.taskdefs.condition.Os.*

def isWindows = isFamily(FAMILY_WINDOWS)

String pg_username = "postgres"
String pg_password = "postgres"
String pg_db_name = "test_db"
int pg_port = 54321


postgresEmbedded {
	version = "V9_6_3"
	host = "localhost"
	port = pg_port
	dbName = pg_db_name
	username = pg_username
	password = pg_password
	artifactStorePath = "./.embedpostgresql" // where PostgreSQL distributions are stored after downloading, inside home directory by default
	stopWhenBuildFinished = true
	timeoutMillisBeforeStop = isWindows ? 15000 : 0
}

String pg_connection="postgres://${pg_username}:${pg_password}@localhost:${pg_port}/${pg_db_name}"
String projectDir=project.projectDir.toString()
String buildDir="${projectDir}/build"
String testOutputDir="${buildDir}/test"

int test_timeout = 15000

////////////////////////////////////////////
task npmBootstrap (type: CrossPlatformExec) {
  commandLine 'npm', 'install'
}

task toolsBootstrap (type: CrossPlatformExec) {
  commandLine 'npm', 'install'
  workingDir './tools/tymly-packager'
}

task cleanNodeModules (type: CrossPlatformExec,
                       dependsOn: [npmBootstrap]) {
  commandLine 'npm', 'run', 'clean'
}

task cleanBuild (type: Delete) {
  delete buildDir
}

task makeTestOutputDir {
  mkdir testOutputDir
}

String tarballDir = "${buildDir}/tarball"
String tarfileName = "${tarballDir}/tymly-${getDate()}.tgz"
def getDate() { new Date().format('yyyyMMdd-HHmmss') }

task createTarball (type: CrossPlatformExec,
                    dependsOn: [npmBootstrap, toolsBootstrap]) {
  doFirst {
    mkdir tarballDir
  }

  def prefix = isWindows ? '' : './'
  commandLine "${prefix}bundle", "${projectDir}", '--tar', tarfileName
  workingDir './tools/tymly-packager/bin'
}

String coreDockerDir = './tools/tymly-dockerize-core'
String coreDockerTag = 'tymly-core'
String serverDockerDir = './tools/tymly-dockerize-server'
String serverDockerTag = 'tymly'

task dropTarfileToDocker (type: Copy,
                          dependsOn: [createTarball]) {
  from tarfileName
  into coreDockerDir
  rename { fileName -> "bundle.tgz" }
} // dropTarfileToDocker

task dockerBuildCore (type: CrossPlatformExec,
                      dependsOn: [dropTarfileToDocker]) {
  workingDir coreDockerDir
  commandLine 'docker', 'build', '--tag', coreDockerTag, '.'
} // dockerBuildCore

task dockerBuildServer (type: CrossPlatformExec,
                        dependsOn: [dockerBuildCore]) {
  workingDir serverDockerDir
  commandLine 'docker', 'build', '--tag', serverDockerTag, '.'
} // dockerBuildServer

task dockerTagCore (type: CrossPlatformExec) {
  commandLine 'docker', 'tag', coreDockerTag, "wmfs/${coreDockerTag}"
} // dockerTagCore

task dockerTagServer (type: CrossPlatformExec) {
  commandLine 'docker', 'tag', serverDockerTag, "wmfs/${serverDockerTag}"
} // dockerTagServer

task dockerPushCore (type: CrossPlatformExec,
                     dependsOn: [dockerTagCore]) {
  commandLine 'docker', 'push', "wmfs/${coreDockerTag}"
} // dockerPushCore

task dockerPushServer (type: CrossPlatformExec,
                       dependsOn: [dockerTagServer]) {
  commandLine 'docker', 'push', "wmfs/${serverDockerTag}"
} // dockerPushServer

////////////////////////////////////////////
task cleanTestFiles (type: Delete) {
  group 'Clean'
  description 'Cleans up TEST-*-results.xml files'

  delete testOutputDir
}
task clean (dependsOn: [cleanNodeModules, cleanTestFiles, cleanBuild]) {
  group 'Clean'
  description 'Discards all node_modules directories, cleans up TEST-*.xml files, removes build directory'
}

task bootstrap (type: CrossPlatformExec,
                dependsOn: [npmBootstrap]) {
  group 'Build'
  description 'Installs the npm dependencies'

  commandLine 'npm', 'run', 'bootstrap'
}

def packages = []
['packages', 'plugins', 'blueprints'].each { d -> new File("./${d}").traverse(type: DIRECTORIES, maxDepth: 0) { packages << [it.name, it.toString()] } }
def testTasks = []
def ci_testTasks = []
packages.each { p ->
  def name = p[0]
  def dir = p[1]
  def taskName = "test${name.capitalize()}"

  testTasks << taskName
  ci_testTasks << "ci_${taskName}"

  task "${taskName}"(type: CrossPlatformExec,
           dependsOn: [bootstrap, startPostgres]) {
      group 'Test'
      description "Runs ${name} test suite"

      commandLine 'npm', 'run', 'test'
      workingDir "${dir}"

      environment PG_CONNECTION_STRING: pg_connection
      environment TIMEOUT: test_timeout
  }

  task "ci_${taskName}"(type: CrossPlatformExec,
                        dependsOn: [bootstrap, cleanTestFiles, makeTestOutputDir, startPostgres]) {
    commandLine 'npm', 'run', 'test', '--', '--reporter', 'xunit', '--reporter-options', "output=${testOutputDir}/TEST-${name}-result.xml"
    workingDir "${dir}"

    environment PG_CONNECTION_STRING: pg_connection
    environment TIMEOUT: test_timeout
  }
}

task test (dependsOn: testTasks) {
  group 'Build'
  description 'Runs whole test suite'
}

task ci_test (dependsOn: ci_testTasks) {
  group 'Build'
  description 'Runs whole test suite, generating JUnit style XML output'
}

task bundle (dependsOn: [createTarball, dropTarfileToDocker]) {
  group 'Build'
  description 'Creates an installable Tymly package'
} // bundle

task dockerBuild (dependsOn: [dockerBuildServer]) {
  group 'Build'
  description 'Build a runnable Tymly Docker image'
} // dockerBuild

task dockerPush (dependsOn: [dockerPushCore, dockerPushServer]) {
  group 'Build'
  description 'Push Tymly Docker images to Hub'
} // dockerPush
